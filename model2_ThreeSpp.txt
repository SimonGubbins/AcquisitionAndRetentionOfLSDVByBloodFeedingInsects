model{

# For each observation ...
  for(j in 1:nObs){

# Probability of aquisition
    logit(pA[j])<-a[Y[j,4]]+b[Y[j,3],Y[j,2]]

# Probability of retention
    pR[j]<-exp(-g[Y[j,3],Y[j,2]]*Y[j,6])

# Probability of being positive
    pPos[j]<-pA[j]*pR[j]

# Likelihood
    Y[j,7]~dbern(pPos[j])

  }

# Hierarchical priors for probability of acquisition
# Source (1-normal skin, 2-lesion, 3-haemotek)
  for(k in 1:3){
    a[k]~dnorm(0,0.01)
  }

# Insect part (1-proboscis, 2-head/thorax, 3-abdomen)
  for(s in 1:nSpp){
    b[1,s]<-0
    b[2,s]~dnorm(mu.b[2],tau.b[2])
    b[3,s]~dnorm(mu.b[3],tau.b[3])
  }
  mu.b[1]<-0
  mu.b[2]~dnorm(0,0.01)
  mu.b[3]~dnorm(0,0.01)
  tau.b[1]<-0
  tau.b[2]~dexp(0.01)
  tau.b[3]~dexp(0.01)

# Decay rate (varies by insect part)
  for(s in 1:nSpp){
    for(k in 1:3){
      g[k,s]~dgamma(a.g[k],b.g[k])
    }
  }
  for(k in 1:3){
    a.g[k]~dexp(0.01)
    b.g[k]~dexp(0.01)
  }

}
